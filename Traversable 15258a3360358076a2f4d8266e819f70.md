
# Traversable

## こういうコードがあった

複数の企業IDを受け取り、まずその企業を登録している複数の`companyListId`を受け取り、それをもとにダッシュボードの作成者を受け取るというようなコードである。

F#にはasyncコンピュテーション式というものがあり、`let! hoge = getAsyncValue`のように記述するとコンピュテーション式内でhogeはasyncがアンラップされた状態で扱うことができる。

しかし今回のこのコードはエラーが出ている。

`companyListIds`の定義を見てほしい。

`companyListIds`の型が`List<Async<CompanyListIds>>`となっている。

Listが一番最後に外側にいるので、async コンピュテーション式でアンラップすることができない。

```fsharp
let getRecipientIds
  (fetchCompanyListIds: CompanyId -> Async<CompanyListIds>)
  (fetchDashboardAuthors: CompanyListId -> Async<DashboardAuthors>)
  (companyIds: CompanyId list)
  : Async<RecipientIds> =
  async {
      let! companyListIds =
        companyIds // CompanyId list
        |> List.map fetchCompanyListIds　// Async<CompanyListId> list
         (*ここでList<Async<CompanyListIds>>をAsync<List<CompanyListId>>にしたい*)
        
      let! dashboardAuthors =
        companyListIds
        |> List.map fetchDashboardAuthors
        (*ここでList<Async<DashboardAuthor>>をAsync<List<DashboardAuthor>>にしたい*)

      return RecipientIds(dashboardAuthors |> List.map (fun (DashboardAuthor x) -> RecipientId x))
  }
```

## 結論こう書ける

```fsharp
#r "nuget: FSharpPlus, 1.6.1"
open FSharpPlus

let getRecipientIds
  (fetchCompanyListId: CompanyId -> Async<CompanyListId>)
  (fetchDashboardAuthor: CompanyListId -> Async<DashboardAuthor>)
  (companyIds: CompanyId list)
  : Async<RecipientIds> =
  async {
    let! companyListIds = traverse fetchCompanyListId companyIds
    let! dashboardAuthors = traverse fetchDashboardAuthor companyListIds
    return RecipientIds(dashboardAuthors |> List.map (fun (DashboardAuthor x) -> RecipientId x))
  }

```

## なぜこう書けるのか

### 定義を見てみる

traverseはTraversableに定義されている関数。

Traversableにはtraverseとsequenceの２つが定義されている

![image.png](Traversable%2015258a3360358076a2f4d8266e819f70/image.png)

### Functor…Applicative…Foldable…Traversable

Traversableを理解するにはFunctorとApplicativeとFoldableを理解している必要がある

### Functor(関手)

「ある構造の中のそれぞれの要素に関数を適用する」という考え方の抽象

map関数を提供するデータ構造のこと。

FSharpPlusだとこれらのFunctorに汎用的に使えるmap関数が使えたりする

**定義**

- `Map: (x: 'Functor<'T>, f: 'T -> 'U) : 'Functor<'U>`

Functorの例

- **`seq<'T>`**
- **`list<'T>`**
- **`array<'T>`**
- **`Async<'T>`**
- **`Result<'T,'U>`**
- **`Task<'T>`**

### Applicative

純粋な式を埋め込んだり、一連の計算を行い、その結果を結合する（<*>）操作を提供するFunctor

**定義**

- `Return (x: 'T) : 'Applicative<'T>`
- `(<*>) (f: 'Applicative<'T -> 'U>, x: 'Applicative<'T>) : 'Applicative<'U>`

**その他の関数**

- `Lift2 (f: 'T1 -> 'T2 -> 'T, x1: 'Applicative<'T1>, x2: 'Applicative<'T2>) : 'Applicative<'T>`

Applicativeの例

- **`seq<'T>`**
- **`list<'T>`**
- **`array<'T>`**
- **`Async<'T>`**
- **`Result<'T, 'U>`**
- **`Task<'T>`**

### Foldable

要約された値に畳み込むことができるデータ構造

**最小の定義**

- `ToSeq (x:'Foldable<'T>) :seq<'T>`

**その他の関数**

- `FoldMap (x:'Foldable<'T>, f:'T->'Monoid)`

Foldableの例

- **`seq<'T>`**
- **`list<'T>`**
- **`option<'T>`**
- **`voption<'T>`**

### Traversable

コレクション内の各要素にアクションを実行しながら、左から右へ走査(traverse)できるデータ構造。

**最小の定義**

- `Traverse (t: 'Traversable<'T>, f: 'T -> 'Applicative<'U>) : 'Applicative<'Traversable<'U>>`
- `Sequence (t: 'Traversable<'Applicative<'T>>) : 'Applicative<'Traversable<'T>>`

## 実装してみる

まずList<Async<’a>>をAsync<List<’a>>に変化するinvertという関数を実装してみることにする。

```fsharp
    let getRecipientIds
        (fetchCompanyListIds: CompanyId -> Async<CompanyListIds>)
        (fetchDashboardAuthors: CompanyListId -> Async<DashboardAuthors>)
        (companyIds: CompanyId list)
        : Async<RecipientIds> =
        let rec invert (x: List<Async<'a>>): Async<List<'a>> =
            match x with
            | [] -> failwith "TODO"
            | head::tail -> failwith "TODO"
        async {
            let! companyListIds =
              companyIds
              |> List.map fetchCompanyListIds
              |> invert
              
            let! dashboardAuthors =
              companyListIds
              |> List.map fetchDashboardAuthors
              |> invert
            return RecipientIds(dashboardAuthors |> List.map (fun (DashboardAuthor (x: string)) -> RecipientId x))
        }
```

まずパターンマッチした結果、[]だった場合、Asyncで包まれた空配列を返す

```fsharp
let rec invert (x: List<Async<'a>>) =
  match x with
  | [] -> async.Return []
  | head::tail -> failwith "TODO" 
```

それ以外の場合は、headと再起呼び出ししたinvert tailの結果をリストにして返している

```fsharp
let rec invert (x: List<Async<'a>>):Async<List<'a>> =
            match x with
            | [] -> async.Return []
            | head::tail -> Async.map2 (fun h t -> h::t) head (invert tail)
```

### 野生のApplicativeが飛び出してきた

パターンマッチの下のブランチに注目してほしい。map2はasyncをアプリカティブたらしめるものにしている。 AsyncはアプリカティブなのでApplicativeに定義されているlift2という関数を使うこともできる

```fsharp
let rec invert (x: List<Async<'a>>):Async<List<'a>> =
	  match x with
    | [] -> async.Return []
    | head::tail -> lift2 (fun h t -> h::t) head (invert tail)
```

```fsharp
    let getRecipientIds
        (fetchCompanyListId: CompanyId -> Async<CompanyListId>)
        (fetchDashboardAuthor: CompanyListId -> Async<DashboardAuthor>)
        (companyIds: CompanyId list)
        : Async<RecipientIds> =
        let rec invert (x: List<Async<'a>>):Async<List<'a>> =
            match x with
            | [] -> async.Return []
            | head::tail -> Async.map2 (fun h t -> h::t) head (invert tail)
        async {
            let! companyListIds =
              companyIds
              |> List.map fetchCompanyListId
              |> invert
              
            let! dashboardAuthors =
              companyListIds
              |> List.map fetchDashboardAuthor
              |> invert
            return RecipientIds(dashboardAuthors |> List.map (fun (DashboardAuthor (x: string)) -> RecipientId x))
        }
```

### 野生のsequenceを発見した

私達が先程定義した関数はsequenceとして知られているTranversable型に定義されている関数である。

sequenceは何かしらのラップされた値のコレクションを受け取り、何かしらのラップされたコレクションに変える関数である。fsharp plusにすでにsequenceが定義されているので、これを使うとこのようになる。

```fsharp
    let getRecipientIds
        (fetchCompanyListId: CompanyId -> Async<CompanyListId>)
        (fetchDashboardAuthor: CompanyListId -> Async<DashboardAuthor>)
        (companyIds: CompanyId list)
        : Async<RecipientIds> =
        async {
            let! companyListIds =
              companyIds
              |> List.map fetchCompanyListId
              |> sequence
              
            let! dashboardAuthors =
              companyListIds
              |> List.map fetchDashboardAuthor
              |> sequence
            return RecipientIds(dashboardAuthors |> List.map (fun (DashboardAuthor (x: string)) -> RecipientId x))
        }
```

```fsharp
let rec sequence f x =
            match x with
            | [] -> async.Return []
            | head::tail -> Async.map2 (fun h t -> h::t) (f head) (sequence f tail)
```

```fsharp
    let getRecipientIds
        (fetchCompanyListId: CompanyId -> Async<CompanyListId>)
        (fetchDashboardAuthor: CompanyListId -> Async<DashboardAuthor>)
        (companyIds: CompanyId list)
        : Async<RecipientIds> =
        let rec sequence f  x =
            match x with
            | [] -> async.Return []
            | head::tail -> Async.map2 (fun h t -> h::t) (f head) (sequence f tail)
        async {
            let! companyListIds = sequence fetchCompanyListId companyIds
              
            let! dashboardAuthors = sequence fetchDashboardAuthor companyListIds
            
            return RecipientIds(dashboardAuthors |> List.map (fun (DashboardAuthor (x: string)) -> RecipientId x))
        }
```

### 野生のTraversableが飛び出してきた

さきほど書いたこちらの関数はtraverseと呼ばれているTraversableに定義されている関数。

シーケンスとマッピングの両方を同時に行う場合、関数traverseを呼び出すとスッキリ書けます。

fsharp plusにももちろん定義されているので、こう書けます。

```fsharp
#r "nuget: FSharpPlus, 1.6.1"
open FSharpPlus

```

Traversableはsequenceとtraverseという２つの関数を持つ。

ちなみにsequence はtraverseの特殊ケースの一つで定義はこうされているみたい。

traverseの引数fに`id`という同一性関数をカリー化させたもの

```fsharp
let sequence = traverse id
```

もう一度traverseの定義を見てみる

```fsharp
static member Traverse (t: 'Traversable<'T>, f: 'T -> 'Applicative<'U>) : 'Applicative<'Traversable<'U>>

```

先程見たようにAsyncはApplicativeだった。

TraversableになるにはFunctorかつFoldableである必要がある

Foldableは畳み込み可能なデータ構造のこと

リストはFunctorでもあり、FoldableでもあるのでTraversable。

traverseは `T → Applicative<U>`と`Traversable<T>`を受け取り、Traversableをパターンマッチして一つ一つのApplicativeで包まれている一つ一つの要素にたいして計算を行い、その結果としてApplicative<Traversable<’U>>を返すというような関数

## まとめ

- sequenceが有用なシーン
    - optionやResult、Asyncのようなものでラップされた値のコレクションを持っていて、実際に必要なのはoption<list<'a>>やResult<list<'a>, 'e>、Async<List<’a>,’e>などである場合
- traverseが有用なシーン
    - ラップされた値のコレクションに対して計算を実行する必要がある場合